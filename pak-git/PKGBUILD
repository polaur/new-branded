# Maintainer: nycko123 <nycko123 at gmail dot com>
# Contributor: pavbaranov <pavbaranov at gmail dot com>

# You had to install all optdepends for full functionality of this script

pkgname=pak-git
epoch=1
pkgver=5.7.r1.g9ed046f
pkgrel=1
pkgdesc="Simple pacman wrapper, AUR and POLAUR helper"
arch=('any')
url="https://gitlab.com/nycko123/pak"
license=('GPL')
depends=('bash' 'git' 'curl' 'jq' 'pacman-contrib' 'man-db')
optdepends=("devtools: to download PKGBUILDs of official packages"
			"auracle-git: (AUR) to manage AUR packages"
			"namcap: to validate built packages"
			"reflector: to keep mirrorlist up to date"
			"subversion: to check upstream for updated -svn packages"
			"fzf: to display all data about installed packages"
			"downgrade: (AUR) to downgrade packages"
			"libnotify: to display desktop notifications")
backup=(etc/xdg/pak.conf)
conflicts=(pak)

source=("git+https://gitlab.com/nycko123/pak.git")

sha256sums=('SKIP')

pkgver() {
 cd "${pkgname%-*}"
 git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package(){
 install -Dm755 "$srcdir/${pkgname%-*}/${pkgname%-*}" "$pkgdir/usr/bin/${pkgname%-*}"
 # configuration
 install -Dm644 "$srcdir/${pkgname%-*}/pak.conf" "$pkgdir/etc/xdg/pak.conf"
 # translations
 install -Dm644 "$srcdir/${pkgname%-*}/locale/pl.mo" "$pkgdir/usr/share/locale/pl/LC_MESSAGES/pak.mo"
 # man pages
 install -Dm644 "$srcdir/${pkgname%-*}/man/pak.8.gz" "$pkgdir/usr/share/man/man8/pak.8.gz"
 install -Dm644 "$srcdir/${pkgname%-*}/man/pak.conf.5.gz" "$pkgdir/usr/share/man/man5/pak.conf.5.gz"
 install -Dm644 "$srcdir/${pkgname%-*}/man/pl/pak.8.gz" "$pkgdir/usr/share/man/pl/man8/pak.8.gz"
 install -Dm644 "$srcdir/${pkgname%-*}/man/pl/pak.conf.5.gz" "$pkgdir/usr/share/man/pl/man5/pak.conf.5.gz"
 # bash autocompletions
 install -Dm644 "$srcdir/${pkgname%-*}/completion/pak.bash" "$pkgdir/usr/share/bash-completion/completions/pak"
 # zsh autocompletions
 install -Dm644 "$srcdir/${pkgname%-*}/completion/pak.zsh" "$pkgdir/usr/share/zsh/site-functions/_pak"
 # checkupdates systemd --user service + timer
 install -Dm644 "$srcdir/${pkgname%-*}/systemd-services/pak-checkupdates.service" "$pkgdir/usr/lib/systemd/user/pak-checkupdates.service"
 install -Dm644 "$srcdir/${pkgname%-*}/systemd-services/pak-checkupdates.timer" "$pkgdir/usr/lib/systemd/user/pak-checkupdates.timer"
 # auracle-git systemd --user service + timer
 install -Dm644 "$srcdir/${pkgname%-*}/systemd-services/pak-auracle.service" "$pkgdir/usr/lib/systemd/user/pak-auracle.service"
 install -Dm644 "$srcdir/${pkgname%-*}/systemd-services/pak-auracle.timer" "$pkgdir/usr/lib/systemd/user/pak-auracle.timer"
 # pacman hooks
 install -Dm644 "$srcdir/${pkgname%-*}/libalpm-hooks/pak-checkupdates.hook" "$pkgdir/usr/share/libalpm/hooks/pak-checkupdates.hook"
 install -Dm644 "$srcdir/${pkgname%-*}/libalpm-hooks/pak-auracle.hook" "$pkgdir/usr/share/libalpm/hooks/pak-auracle.hook"
 # set fs.protected_regular = 0
 install -Dm644 "$srcdir/${pkgname%-*}/libalpm-hooks/99-pak-sysctl.conf" "$pkgdir/usr/lib/sysctl.d/99-pak-sysctl.conf"
}
