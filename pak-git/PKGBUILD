# Maintainer: nycko123 <nycko123 at gmail dot com>
# Contributor: pavbaranov <pavbaranov at gmail dot com>

# You had to install all optdepends for full functionality of this script
 
pkgname=pak-git
pkgver=2.1.r6.g49dd065
pkgrel=2
pkgdesc="Simple pacman and AUR frontend"
arch=('any')
url="https://gitlab.com/nycko123/pak"
license=('GPL')
depends=('bash' 'git' 'jq' 'pacman-contrib')
optdepends=("asp: to download PKGBUILD's of official packages"
            "auracle-git: to manage AUR packages"
            "namcap: to validate built packages"
			"reflector: to keep mirrorlist up to date"
			"fzf: to display all data about installed packages")
backup=('etc/xdg/pak.conf')
conflicts=(pak)
 
source=("git+https://gitlab.com/nycko123/pak.git")
 
sha256sums=('SKIP')

pkgver() {
 cd "${pkgname%-*}"
 git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}
 
package(){
 install -Dm755 "$srcdir/${pkgname%-*}/${pkgname%-*}" "$pkgdir/usr/bin/${pkgname%-*}"
 # configuration
 install -Dm644 "$srcdir/${pkgname%-*}/pak.conf" "$pkgdir/etc/xdg/pak.conf"
 # translations
 install -Dm644 "$srcdir/${pkgname%-*}/locale/pl.mo" "$pkgdir/usr/share/locale/pl/LC_MESSAGES/pak.mo"
 # man-pages
 install -Dm644 "$srcdir/${pkgname%-*}/man/pak.8.gz" "$pkgdir/usr/share/man/man8/pak.8.gz"
 install -Dm644 "$srcdir/${pkgname%-*}/man/pak.conf.5.gz" "$pkgdir/usr/share/man/man5/pak.conf.5.gz"
 install -Dm644 "$srcdir/${pkgname%-*}/man/pl/pak.conf.5.gz" "$pkgdir/usr/share/man/pl/man5/pak.conf.5.gz"
 # bash autocompletions
 install -Dm644 "$srcdir/${pkgname%-*}/completion/pak.bash" "$pkgdir/usr/share/bash-completion/completions/pak"
 # zsh autocompletions
 install -Dm644 "$srcdir/${pkgname%-*}/completion/pak.zsh" "$pkgdir/usr/share/zsh/site-functions/_pak"
}
