# Maintainer: nycko123 <nycko123 at gmail dot com>
# Contributor: pavbaranov <pavbaranov at gmail dot com>

# You had to install all optdepends for full functionality of this script
 
pkgname=pak-git
pkgver=1.9.r1.gbd867f6
pkgrel=1
pkgdesc="Simple pacman and AUR frontend"
arch=('any')
url="https://gitlab.com/nycko123/pak"
license=('GPL')
depends=('bash' 'git' 'jq')
optdepends=("asp: to download PKGBUILD's of official packages"
            "cower: to manage AUR packages"
            "namcap: to validate built packages"
			"reflector: to keep mirrorlist up to date")
backup=('etc/xdg/pak.conf')
conflicts=(pak)
 
source=("git+https://gitlab.com/nycko123/pak.git")
 
sha256sums=('SKIP')

pkgver() {
 cd "${pkgname%-*}"
 git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}
 
package(){
 install -Dm755 "$srcdir/${pkgname%-*}/${pkgname%-*}" "$pkgdir/usr/bin/${pkgname%-*}"
 install -Dm644 "$srcdir/${pkgname%-*}/pak.conf" "$pkgdir/etc/xdg/pak.conf"
 install -Dm644 "$srcdir/${pkgname%-*}/locale/pl.mo" "$pkgdir/usr/share/locale/pl/LC_MESSAGES/pak.mo"
 install -Dm644 "$srcdir/${pkgname%-*}/man/pak.8.gz" "$pkgdir/usr/share/man/man8/pak.8.gz"
 install -Dm644 "$srcdir/${pkgname%-*}/man/pak.conf.5.gz" "$pkgdir/usr/share/man/man5/pak.conf.5.gz"
}
