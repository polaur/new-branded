# Maintainer: pavbaranov <pavbaranov@gmail.com>
# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
 
pkgname=kooka-git
pkgver=0.61.810.g0cff6a5
pkgrel=1
pkgdesc='The scanning application and libkscan library for Plasma 6'
arch=('x86_64')
url="https://userbase.kde.org/Kooka"
license=('GPL-3')
#depends=('kio' 'sane' 'libpaper' 'purpose' 'kxmlgui' 'ktextwidgets')
depends=('sane' 'kxmlgui' 'kconfigwidgets' 'kconfig' 'kio' 'purpose' 'ki18n' 'kcoreaddons' 'kcrash' 'kcolorscheme' 'kservice' 'libpaper' 'kiconthemes' 'ktextwidgets' 'kwidgetsaddons' 'kcompletion')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
optdepends=('ocrad' 'gocr')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
replaces=("${pkgname%-*}")
source=("git+https://invent.kde.org/graphics/${pkgname%-git}.git")

sha256sums=('SKIP')

pkgver() {
  cd kooka
  git describe --long --tags | sed 's/^kooka.//;s/-/./g'
  }

prepare() {
  [ -d build ] || mkdir build
  }
    
build() {
  cmake -B build -S ${pkgname%-git} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DQT_MAJOR_VERSION=6 \
    -DBUILD_TESTING=OFF
    
  cmake --build build
  }
 
package() {
    DESTDIR="$pkgdir" cmake --install build
  }
