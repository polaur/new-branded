# Maintainer: nycko123 <nycko123 at gmail dot com>
# Contributor: pavbaranov <pavbaranov at gmail dot com>

# You had to install all optdepends for full functionality of this script

pkgname=pak
epoch=1
pkgver=5.6
pkgrel=1
pkgdesc="Simple pacman wrapper, AUR and POLAUR helper"
arch=('any')
url="https://gitlab.com/nycko123/pak"
license=('GPL')
depends=('bash' 'git' 'curl' 'jq' 'pacman-contrib' 'man-db')
optdepends=("devtools: to download PKGBUILD's of official packages"
            "auracle-git: (AUR) to manage AUR packages"
            "namcap: to validate built packages"
            "reflector: to keep mirrorlist up to date"
            "subversion: to check upstream for updated -svn packages"
            "fzf: to display all data about installed packages"
            "downgrade: (AUR) to downgrade packages"
            "libnotify: to display desktop notifications")
backup=(etc/xdg/pak.conf)
changelog="CHANGELOG"
conflicts=(pak-git)

source=("https://gitlab.com/nycko123/pak/raw/master/src/${pkgname}-${pkgver}.tar.gz")

sha256sums=('e2f9d8afe833690319a27844a909c44db6ab7e4b87601ccb0ce5af313ea3a06e')

package(){
 install -Dm755 "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"
 # configuration
 install -Dm644 "$srcdir/pak.conf" "$pkgdir/etc/xdg/pak.conf"
 # translations
 install -Dm644 "$srcdir/locale/pl.mo" "$pkgdir/usr/share/locale/pl/LC_MESSAGES/pak.mo"
 # man pages
 install -Dm644 "$srcdir/man/pak.8.gz" "$pkgdir/usr/share/man/man8/pak.8.gz"
 install -Dm644 "$srcdir/man/pak.conf.5.gz" "$pkgdir/usr/share/man/man5/pak.conf.5.gz"
 install -Dm644 "$srcdir/man/pl/pak.8.gz" "$pkgdir/usr/share/man/pl/man8/pak.8.gz"
 install -Dm644 "$srcdir/man/pl/pak.conf.5.gz" "$pkgdir/usr/share/man/pl/man5/pak.conf.5.gz"
 # bash autocompletions
 install -Dm644 "$srcdir/completion/pak.bash" "$pkgdir/usr/share/bash-completion/completions/pak"
 # zsh autocompletions
 install -Dm644 "$srcdir/completion/pak.zsh" "$pkgdir/usr/share/zsh/site-functions/_pak"
 # checkupdates systemd --user service + timer
 install -Dm644 "$srcdir/systemd-services/pak-checkupdates.service" "$pkgdir/usr/lib/systemd/user/pak-checkupdates.service"
 install -Dm644 "$srcdir/systemd-services/pak-checkupdates.timer" "$pkgdir/usr/lib/systemd/user/pak-checkupdates.timer"
 # auracle-git systemd --user service + timer
 install -Dm644 "$srcdir/systemd-services/pak-auracle.service" "$pkgdir/usr/lib/systemd/user/pak-auracle.service"
 install -Dm644 "$srcdir/systemd-services/pak-auracle.timer" "$pkgdir/usr/lib/systemd/user/pak-auracle.timer"
}
